#include <intersections>
#include <structures>

layout (std430, binding = 2) buffer Triangles {
	PackedTriangle data[];
} triangles;

layout(std430, binding = 3) buffer Nodes {
	BVHNode data[];
}nodes;

void CheckLeaf(Ray ray, uint nodePtr, float tmin, float tmax,
	inout int triangleIndex, inout vec3 intersection);

void QueryBVH(Ray ray, float tmin, float tmax, out int triangleIndex,
	out vec3 intersection) {
		
	uint stack[32];
	uint stackPtr = uint(1);
	
	intersection.x = tmax;
	
	BVHNode node = nodes.data[0];
	
	do {
		// If the pointer is -1 then there is no child.
		uint childLPtr = node.data1;
		uint childRPtr = node.data2;
		
		bool intersectL = false;
		bool intersectR = false;
		bool isLLeaf = false;
		bool isRLeaf = false;
		bool validL = false;
		bool validR = false;
		
		float tL = 0.0;
		float tR = 0.0;
		
		if (childLPtr > uint(0)) {
			intersectL = Intersection(ray, 
				nodes.data[childLPtr].aabb, tmin, tmax, tL);
			isLLeaf = (nodes.data[childLPtr].data2 & uint(0x80000000)) > uint(0);
			validL = tL < intersection.x;
		}
		
		if (childRPtr >	uint(0)) {
			intersectR = Intersection(ray, 
				nodes.data[childRPtr].aabb, tmin, tmax, tR);
			isRLeaf = (nodes.data[childRPtr].data2 & uint(0x80000000)) > uint(0);
			validR = tR < intersection.x;
		}
		
		bool traverseL = (intersectL && !isLLeaf && validL);
		bool traverseR = (intersectR && !isRLeaf && validR);
		
		if(intersectL && isLLeaf && validL) {
			CheckLeaf(ray, childLPtr, tmin, tmax, 
				triangleIndex, intersection);
		}
		
		if (intersectR && isRLeaf && validR) {
			CheckLeaf(ray, childRPtr, tmin, tmax,
				triangleIndex, intersection);
		}
		
		if (!traverseL && !traverseR) {
			node = nodes.data[stack[--stackPtr]];
		}
		else {
			node = traverseL ? nodes.data[childLPtr] :
				nodes.data[childRPtr];
			if (traverseL && traverseR)
				stack[stackPtr++] = childRPtr;
		}
			
	}
	while (stackPtr != uint(0));
		
}

void CheckLeaf(Ray ray, uint nodePtr, float tmin, float tmax,
	inout int triangleIndex, inout vec3 intersection) {
		
	uint offset = nodes.data[nodePtr].data1;
	uint count = nodes.data[nodePtr].data2 & uint(0x7fffffff);
	
	vec3 sol, v0, v1, v2;
	
	for (uint i = offset; i < offset + count; i++) {
		v0 = triangles.data[i].v0.xyz;
		v1 = triangles.data[i].v1.xyz;
		v2 = triangles.data[i].v2.xyz;
		if (Intersection(ray, v0, v1, v2, sol)) {
			if (sol.x < intersection.x) {
				intersection = sol;
				triangleIndex = int(i);
			}
		}
	}
		
}